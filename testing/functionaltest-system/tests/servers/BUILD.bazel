load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

alias(
    name = "helloworld_server",
    actual = "@com_github_grpc_grpc//examples/cpp/helloworld:greeter_server",
)

alias(
    name = "route_guide_server",
    actual = "@com_github_grpc_grpc//examples/cpp/route_guide:route_guide_server",
)

alias(
    name = "grpc_cli",
    actual = "@com_github_grpc_grpc//test/cpp/util:grpc_cli",
)

pkg_files(
    name = "server_executables",
    srcs = [
        ":helloworld_server",
        ":route_guide_server",
    ],
    attributes = pkg_attributes(mode = "0755"),
    prefix = "/server/bin",
)

pkg_files(
    name = "server_data",
    srcs = [
        "@com_github_grpc_grpc//examples/cpp/route_guide:route_guide_db.json",
    ],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "/server/data",
)

pkg_tar(
    name = "server_binaries",
    srcs = [
        ":server_data",
        ":server_executables",
    ],
    package_dir = "/",
)

oci_image(
    name = "grpc_examples",
    base = select({
        "@platforms//cpu:aarch64": "@runtime-debian-arm64",
        "@platforms//cpu:x86_64": "@runtime-debian-amd64",
    }),
    entrypoint = ["/bin/bash"],
    labels = {
        "org.opencontainers.image.description": "gRPC servers used for testing the functionaltest-system",
        "org.opencontainers.image.title": "gRPC example servers",
    },
    tars = [
        ":server_binaries.tar",
    ],
)

server_image_uri = "grpc_examples_docker_image:latest"

oci_tarball(
    name = "tarball",
    image = ":grpc_examples",
    repo_tags = [server_image_uri],
)

genrule(
    name = "copy_to_dist",
    srcs = [
        ":tarball",
        ":grpc_cli",
    ],
    outs = ["copy_to_dist.bin"],
    cmd_bash = """cat <<EOF >'$@'
mkdir -p dist/test_servers dist/debian
cp $(execpath :tarball) dist/test_servers/grpc_examples.tar
printf "{}" >dist/test_servers/grpc_examples_uri.txt
cp $(execpath :grpc_cli) dist/debian
chmod 755 dist/debian/grpc_cli
builders/tools/normalize-dist
EOF""".format(server_image_uri),
    executable = True,
    local = True,
)

genrule(
    name = "docker_load_image",
    srcs = [
        ":tarball",
    ],
    outs = ["docker_load_image.bin"],
    cmd_bash = """cat << EOF > '$@'
docker load -i $(execpath :tarball) &>/dev/null
EOF""",
    executable = True,
    local = True,
)

container_structure_test(
    name = "test",
    configs = ["test.yaml"],
    image = ":grpc_examples",
)
