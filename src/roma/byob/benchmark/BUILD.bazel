# Copyright 2024 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load(
    "@com_google_googleapis_imports//:imports.bzl",
    "cc_proto_library",
)
load("@rules_buf//buf:defs.bzl", "buf_lint_test")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files", "pkg_mkdirs")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_proto//proto:defs.bzl", "proto_descriptor_set", "proto_library")
load("//src/roma/tools/api_plugin:roma_api.bzl", "roma_byob_image")
load("//third_party:container_deps.bzl", "DISTROLESS_USERS")

package(default_visibility = ["//visibility:public"])

proto_library(
    name = "traffic_generator_proto",
    srcs = ["traffic_generator.proto"],
    deps = [
        "@com_google_protobuf//:descriptor_proto",
        "@com_google_protobuf//:duration_proto",
    ],
)

cc_proto_library(
    name = "traffic_generator_cc_proto",
    deps = [
        ":traffic_generator_proto",
    ],
)

buf_lint_test(
    name = "traffic_generator_proto_lint",
    size = "small",
    config = "//src:buf.yaml",
    targets = [
        ":traffic_generator_proto",
    ],
)

proto_descriptor_set(
    name = "traffic_generator_descriptor_set",
    deps = [
        ":traffic_generator_proto",
    ],
)

cc_library(
    name = "latency_formatter",
    hdrs = ["latency_formatter.h"],
    deps = [
        "//src/roma/interface",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/time",
    ],
)

cc_library(
    name = "burst_generator",
    srcs = ["burst_generator.cc"],
    hdrs = ["burst_generator.h"],
    deps = [
        ":latency_formatter",
        ":traffic_generator_cc_proto",
        "//src/util:duration",
        "@com_google_absl//absl/functional:any_invocable",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/time",
        "@com_google_protobuf//:protobuf",
    ],
)

cc_library(
    name = "roma_byob_rpc_factory",
    hdrs = ["roma_byob_rpc_factory.h"],
    deps = [
        "//src/roma/byob/config",
        "//src/roma/byob/interface:roma_service",
        "//src/roma/byob/sample_udf:sample_byob_sdk_cc_proto",
        "//src/util:execution_token",
        "//src/util/status_macro:status_macros",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/time",
    ],
)

cc_binary(
    name = "traffic_generator",
    srcs = ["traffic_generator.cc"],
    deps = [
        ":burst_generator",
        ":roma_byob_rpc_factory",
        "//src/communication:json_utils",
        "//src/roma/byob/config",
        "//src/roma/byob/interface:roma_service",
        "//src/roma/byob/sample_udf:sample_byob_sdk_cc_proto",
        "//src/roma/tools/v8_cli:roma_v8_rpc_factory",
        "//src/util:periodic_closure",
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/log:check",
        "@com_google_absl//absl/log:flags",
        "@com_google_absl//absl/log:globals",
        "@com_google_absl//absl/log:initialize",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/time",
        "@com_google_protobuf//:protobuf",
    ],
)

pkg_files(
    name = "traffic_generator_execs",
    srcs = [":traffic_generator"],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/server/bin",
)

[
    pkg_tar(
        name = "traffic_generator_tar_{}".format(user.flavor),
        srcs = [
            ":traffic_generator_execs",
        ],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

[
    expand_template(
        name = "traffic_generator_{}_image_test_yaml".format(user.flavor),
        out = "traffic_generator_{}_image_test.yaml".format(user.flavor),
        substitutions = {
            "{GID}": str(user.gid),
            "{UID}": str(user.uid),
        },
        template = ":traffic_generator_image_test.yaml",
    )
    for user in DISTROLESS_USERS
]

"""
(Optional) Step 1: Make ProcessRequest in dispatcher.h async by removing wait
  for worker and returning early with an error.
Step 2: Run the following from a command line:
    builders/tools/bazel-debian build //src/roma/byob/benchmark:traffic_generator_root_image.tar
    docker load -i bazel-bin/src/roma/byob/benchmark/traffic_generator_root_image_tar/tarball.tar
    docker run --privileged --volume="${PWD}"/dist:/tmp/runsc-log --cpus=64 --memory=512gb \
    privacy-sandbox/roma-byob/traffic_generator:v1-root \
      --num_workers=84 --queries_per_second=10 --burst_size=14 \
      --num_queries=1000 --sandbox=minimal --syscall_filter=true --lib_mounts="/lib64" \
      --binary_path="/udf/sample_udf"
"""

[
    roma_byob_image(
        name = "traffic_generator_{}_image".format(user.flavor),
        container_structure_test_configs = [":traffic_generator_{}_image_test.yaml".format(user.flavor)],
        debug = True,
        entrypoint = ["/server/bin/traffic_generator"],
        repo_tags = ["privacy-sandbox/roma-byob/traffic_generator:v1-{}".format(user.flavor)],
        tags = [
            "noasan",
            "notsan",
            "noubsan",
        ],
        tars = [
            ":traffic_generator_tar_{}".format(user.flavor),
            "//src/roma/byob/sample_udf:udf_{}_tar".format(user.flavor),
        ],
        user = user,
    )
    for user in DISTROLESS_USERS
]

cc_binary(
    name = "memory_usage_benchmark",
    srcs = ["memory_usage_benchmark.cc"],
    deps = [
        "//src/roma/byob/sample_udf:sample_byob_sdk_cc_proto",
        "//src/roma/byob/sample_udf:sample_byob_sdk_roma_cc_lib",
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/log:check",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/time",
        "@nlohmann_json//:lib",
    ],
)

pkg_files(
    name = "memory_usage_benchmark_execs",
    srcs = [":memory_usage_benchmark"],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/server/bin",
)

[
    pkg_tar(
        name = "memory_usage_benchmark_tar_{}".format(user.flavor),
        srcs = [
            ":memory_usage_benchmark_execs",
        ],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

pkg_files(
    name = "sort_list_binaries_execs",
    srcs = [
        "//src/roma/byob/sample_udf:sort_list_100k_udf",
        "//src/roma/byob/sample_udf:sort_list_10k_udf",
        "//src/roma/byob/sample_udf:sort_list_1m_udf",
    ],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/udf",
)

[
    pkg_tar(
        name = "sort_list_binaries_{}_tar".format(user.flavor),
        srcs = [":sort_list_binaries_execs"],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

[
    expand_template(
        name = "memory_usage_benchmark_{}_image_test_yaml".format(user.flavor),
        out = "memory_usage_benchmark_{}_image_test.yaml".format(user.flavor),
        substitutions = {
            "{GID}": str(user.gid),
            "{UID}": str(user.uid),
        },
        template = ":memory_usage_benchmark_image_test.yaml",
    )
    for user in DISTROLESS_USERS
]

# builders/tools/bazel-debian build //src/roma/byob/benchmark:memory_usage_benchmark_root_image.tar
# docker load -i bazel-bin/src/roma/byob/benchmark/memory_usage_benchmark_root_image_tar/tarball.tar
# docker run --privileged --volume="${PWD}"/dist:/data privacy-sandbox/roma-byob/memory_usage_benchmark:v1-root --num_workers 10 --execute_iterations 10 --output=/data/output.csv
[
    roma_byob_image(
        name = "memory_usage_benchmark_{}_image".format(user.flavor),
        container_structure_test_configs = [":memory_usage_benchmark_{}_image_test.yaml".format(user.flavor)],
        debug = True,
        entrypoint = ["/server/bin/memory_usage_benchmark"],
        repo_tags = ["privacy-sandbox/roma-byob/memory_usage_benchmark:v1-{}".format(user.flavor)],
        tags = [
            "noasan",
            "notsan",
            "noubsan",
        ],
        tars = [
            ":memory_usage_benchmark_tar_{}".format(user.flavor),
            "sort_list_binaries_{}_tar".format(user.flavor),
        ],
        user = user,
    )
    for user in DISTROLESS_USERS
]

cc_binary(
    name = "roma_byob_benchmark",
    srcs = ["roma_byob_benchmark.cc"],
    deps = [
        "//src/roma/byob/config",
        "//src/roma/byob/sample_udf:sample_byob_sdk_cc_proto",
        "//src/roma/byob/sample_udf:sample_byob_sdk_roma_cc_lib",
        "//src/roma/byob/sample_udf:sample_callback_cc_proto",
        "//src/roma/byob/utility:utils",
        "//src/roma/config:function_binding_object_v2",
        "@com_github_grpc_grpc//:grpc++",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/log:check",
        "@com_google_absl//absl/log:initialize",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@google_benchmark//:benchmark",
    ],
)

pkg_mkdirs(
    name = "benchmark_distdir",
    attributes = pkg_attributes(mode = "0700"),
    dirs = ["/dist/benchmarks"],
)

pkg_files(
    name = "benchmark_execs",
    srcs = [":roma_byob_benchmark"],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/server/bin",
)

[
    pkg_tar(
        name = "benchmark_tar_{}".format(user.flavor),
        srcs = [
            ":benchmark_distdir",
            ":benchmark_execs",
        ],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

[
    expand_template(
        name = "image_{}_test_yaml".format(user.flavor),
        out = "image_{}_test.yaml".format(user.flavor),
        substitutions = {
            "{GID}": str(user.gid),
            "{UID}": str(user.uid),
        },
        template = ":image_test.yaml",
    )
    for user in DISTROLESS_USERS
]

[
    roma_byob_image(
        name = "benchmark_{flavor}{dbg}_image".format(
            dbg = debug_str,
            flavor = user.flavor,
        ),
        container_structure_test_configs = [
            ":image_{}_test.yaml".format(user.flavor),
        ],
        debug = len(debug_str) > 0,
        entrypoint = [
            "/server/bin/roma_byob_benchmark",
            "--benchmark_counters_tabular=true",
            "--benchmark_time_unit=ms",
        ],
        env = {
            "BENCHMARK_OUT": "/tmp/benchmark_out.json",
            "BENCHMARK_OUT_FORMAT": "json",
        },
        labels = {"tee.launch_policy.log_redirect": "always"},
        repo_tags = [
            "privacy-sandbox/roma-byob/microbenchmarks:v1-{flavor}{dbg}".format(
                dbg = debug_str.replace("_", "-"),
                flavor = user.flavor,
            ),
        ],
        tars = [
            ":benchmark_tar_{}".format(user.flavor),
            "//src/roma/byob/sample_udf:udf_{}_tar".format(user.flavor),
        ],
        user = user,
    )
    for debug_str in ("_debug", "")
    for user in DISTROLESS_USERS
]

"""
Sample usage:

builders/tools/bazel-debian run //src/roma/byob/benchmark:copy_to_dist
docker load -i dist/roma_byob/benchmark_root_image.tar
docker run -it --privileged privacy-sandbox/roma-byob/microbenchmarks:v1-root
"""

genrule(
    name = "copy_to_dist",
    srcs = [
        ":benchmark_nonroot_image.tar",
        ":benchmark_root_image.tar",
        ":benchmark_nonroot_debug_image.tar",
        ":benchmark_root_debug_image.tar",
    ],
    outs = ["copy_to_dist.bin"],
    cmd_bash = """cat <<EOF >'$@'
mkdir -p {dir}
cp $(execpath :benchmark_nonroot_image.tar) {dir}/benchmark_nonroot_image.tar
cp $(execpath :benchmark_root_image.tar) {dir}/benchmark_root_image.tar
cp $(execpath :benchmark_nonroot_debug_image.tar) {dir}/benchmark_nonroot_debug_image.tar
cp $(execpath :benchmark_root_debug_image.tar) {dir}/benchmark_root_debug_image.tar
builders/tools/normalize-dist
EOF""".format(dir = "dist/roma_byob"),
    executable = True,
    local = True,
    message = "copying aws artifacts to dist directory",
)
