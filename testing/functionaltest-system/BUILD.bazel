# Copyright 2023 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files")

exports_files(
    glob(["*.bzl"]),
    visibility = ["//docs/bazel:__pkg__"],
)

bzl_library(
    name = "deps",
    srcs = ["deps.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "@aspect_bazel_lib//lib:repositories",
        "@bazel_gazelle//:deps",
        "@bazel_tools//tools/build_defs/repo:git_worker.bzl",
        "@io_bazel_rules_go//go:deps",
        "@rules_buf//buf:repositories",
        "@rules_pkg//pkg:deps.bzl",
    ],
)

bzl_library(
    name = "repos",
    srcs = ["repos.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "@bazel_tools//tools/build_defs/repo:http.bzl",
        "@bazel_tools//tools/build_defs/repo:utils.bzl",
    ],
)

write_file(
    name = "empty_file",
    out = "empty",
    content = [],
)

write_file(
    name = "sut_workspace_file",
    out = "WORKSPACE.sut",
    content = [],
)

functest_path = "/functionaltest-system"

pkg_files(
    name = "root_files",
    srcs = [
        "repos.bzl",
        ":empty_file",
        ":sut_workspace_file",
    ],
    attributes = pkg_attributes(mode = "0644"),
    prefix = functest_path,
    renames = {
        "WORKSPACE.sut": "WORKSPACE",
        "empty": "BUILD.bazel",
    },
)

pkg_files(
    name = "sut_files",
    srcs = [
        ":empty_file",
        "//sut:testrunner_files",
    ],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "{}/sut".format(functest_path),
    renames = {
        "empty": "BUILD.bazel",
    },
)

pkg_files(
    name = "bazel_binaries",
    srcs = ["//bazel:testrunner_binaries"],
    attributes = pkg_attributes(mode = "0755"),
    prefix = "{}/bazel".format(functest_path),
)

pkg_files(
    name = "bazel_files",
    srcs = ["//bazel:testrunner_files"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "{}/bazel".format(functest_path),
)

pkg_filegroup(
    name = "functionaltest-embed",
    srcs = [
        ":bazel_binaries",
        ":bazel_files",
        ":root_files",
        ":sut_files",
    ],
    visibility = ["//sut:__subpackages__"],
)

exports_files([
    "buf.yaml",
])
